# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-imageformats-debug
_orig_pkgname=${pkgname/-debug/}
_qtver=5.9.0
pkgver=${_qtver/-/}
pkgrel=1
arch=('i686' 'x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Plugins for additional image formats: TIFF, MNG, TGA, WBMP'
depends=('qt5-base' 'jasper' 'libmng' 'libwebp')
makedepends=()
groups=('qt' 'qt5')
_pkgfqn="${_orig_pkgname/5-/}-opensource-src-${_qtver}"
provides=("qt5-imageformats=$pkgver")
options=("debug" "!strip")
conflicts=('qt5-imageformats')
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('2461795047194bddabdf768af862ba374db58418a7c3e26019a8d5f1e7a85481')

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
