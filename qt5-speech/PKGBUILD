# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt5-speech-debug
_orig_pkgname=${pkgname/-debug/}
_qtver=5.8.0
pkgver=${_qtver/-/}
pkgrel=1
arch=(i686 x86_64)
url='http://qt-project.org/'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Qt module to make text to speech and speech recognition easy'
depends=(qt5-multimedia speech-dispatcher)
makedepends=(flite)
optdepends=('flite: flite TTS backend')
groups=(qt qt5)
_pkgfqn="${_orig_pkgname/5-/}-opensource-src-${_qtver}"
provides=("qt5-speech=$pkgver")
options=("debug" "!strip")
conflicts=('qt5-speech')
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
md5sums=('021bcba221e4d175bf4368ee6a19296d')

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base-debug "$pkgdir"/usr/share/licenses/${pkgname}
}
