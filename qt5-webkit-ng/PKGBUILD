# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qt5-webkit-ng
_qtver=tp5
pkgver=${_qtver/-/}
pkgrel=5
arch=(x86_64)
url='https://github.com/annulen/webkit/wiki'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for a WebKit2 based implementation and a new QML API (maintained fork)'
depends=(qt5-location qt5-sensors qt5-webchannel libwebp libxslt libxcomposite gst-plugins-base hyphen)
makedepends=(cmake ruby gperf python2 qt5-doc qt5-tools)
optdepends=('gst-plugins-good: Webm codec support')
conflicts=(qt5-webkit)
provides=(qt5-webkit)
source=("https://github.com/annulen/webkit/archive/qtwebkit-$pkgver.tar.gz"
        qtwebkit-ng-export-cmake-variables.patch::"https://github.com/annulen/webkit/commit/1467a945.patch"
        qt5-webkit-icu59.patch qt5-webkit-ng-gcc7.patch qt5-webkit-ng-functional.patch)
sha256sums=('04bcb819675be6ebe0bba81519319dede6510512a73ef53d20465f91034ee719'
            '4abe2c5f4b99f22a3257f0594819f97abe98b26a0ed1c56555f06ae8e831e49d'
            'e1306e696ad7125fa00083aa966be0ea6fe6de555c9e1d119957f208f258edfe'
            '58ecbd8708f5afe4e974fdcd6c1f32fea61ccc55dc9bb711768f2d0da6963fb8'
            'c85344edcc23930c0190b2f8ec3847bf8bdbbf7f1d4fa877c502e354e5f4233e')

prepare() {
  mkdir -p build

# Correctly export all needed cmake variables
  cd webkit-qtwebkit-$pkgver
  patch -p1 -i ../qtwebkit-ng-export-cmake-variables.patch
# Fix build with ICU 59 (PLD linux)
  patch -p1 -i ../qt5-webkit-icu59.patch
# Fix build with GCC 7
  patch -p1 -i ../qt5-webkit-ng-gcc7.patch
  patch -p1 -i ../qt5-webkit-ng-functional.patch

  sed -e 's|-Wundef|-Wundef -Wno-expansion-to-defined|' -i Source/cmake/WebKitHelpers.cmake
}

build() {
  cd build  

  cmake ../webkit-qtwebkit-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DPORT=Qt \
    -DENABLE_TOOLS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
