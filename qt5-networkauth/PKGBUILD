# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt5-networkauth-debug
_orig_pkgname=${pkgname/-debug/}
_qtver=5.9.0
pkgver=${_qtver/-/}
pkgrel=1
arch=(i686 x86_64)
url='http://qt-project.org/'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Network authentication module'
depends=(qt5-base)
groups=(qt qt5)
_pkgfqn="${_orig_pkgname/5-/}-opensource-src-${_qtver}"
provides=("qt5-networkauth=$pkgver")
options=("debug" "!strip")
conflicts=('qt5-networkauth')
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('0fc08b28f5b3cb2ea84dc9a6f415060b3c957e95171f407cc90ce5f8a9f7eb94')

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
