# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webkit-debug
_orig_pkgname=${pkgname/-debug/}
_qtver=5.8.0
pkgver=${_qtver/-/}
pkgrel=3
arch=('i686' 'x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for a WebKit2 based implementation and a new QML API'
depends=('qt5-location' 'qt5-sensors' 'qt5-webchannel' 'libwebp' 'libxslt' 'libxcomposite' 'gst-plugins-base')
makedepends=('ruby' 'gperf' 'python' 'qt5-doc' 'qt5-tools')
optdepends=('gst-plugins-good: Webm codec support')
conflicts=('qt' 'qt5-webkit')
groups=('qt' 'qt5')
_pkgfqn="${_orig_pkgname/5-/}-opensource-src-${pkgver}"
provides=("qt5-webkit=$pkgver")
options=("debug" "!strip")
source=("http://download.qt.io/community_releases/${pkgver%.*}/${pkgver}-final/qtwebkit-opensource-src-${pkgver}.tar.xz"
        qt5-webkit-icu59.patch)
md5sums=('60a6935aca4a7c553d0ec4646ceed3b4'
         'f6c74b0196a698cc1c5a34c695b22715')

prepare() {
  mkdir -p build

# Fix build with ICU 59 (PLD linux)
  cd ${_pkgfqn}
  patch -p1 -i ../qt5-webkit-icu59.patch
}

build() {
  cd build  

  qmake ../${_pkgfqn}
  make
  make docs
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install install_docs

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base-debug "$pkgdir"/usr/share/licenses/${pkgname}
}
